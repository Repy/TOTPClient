plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    id 'org.beryx.jlink' version '2.18.0'
    // JDKのみにしたい場合は"org.beryx.runtime"を使う
}

group 'info.repy'
version '1.0-SNAPSHOT'

mainClassName = "info.repy.totp/info.repy.totp.TOTPClient"

repositories {
    mavenCentral()
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

run {
    // add-modulesを探す用
    //jvmArgs = ['--limit-modules','java.base,javafx.controls,javafx.fxml,org.json,jdk.crypto.cryptoki']
}

jlink {
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', 'java.base']
    launcher {
        name = 'TOTPClient'
    }
}

dependencies {
    implementation group: 'org.jboss.aerogear', name:'aerogear-otp-java', version: '1.0.+'
}

task buildWindows(type: Zip, dependsOn: jlinkZip) {
    archiveName = "TOTPClient.zip"
    from zipTree(tasks.jlinkZip.imageZip)
    from "scripts/windows/TOTPClient.bat"
}

task buildMacDir(dependsOn: jlinkZip) {
    doLast {
        delete fileTree('build/macapp')
        copy {
            from fileTree("scripts/mac")
            into 'build/macapp/TOTPClient.app'
        }
        copy {
            from zipTree(tasks.jlinkZip.imageZip)
            into 'build/macapp/TOTPClient.app/Contents/MacOS'
        }
    }
}

task buildMac(type: Tar, dependsOn: buildMacDir) {
    compression = Compression.GZIP
    archiveName = "TOTPClient.tar.gz"
    from 'build/macapp'
}

task buildLinux(type: Tar, dependsOn: jlinkZip) {
    compression = Compression.GZIP
    archiveName = "TOTPClient.tar.gz"
    from zipTree(tasks.jlinkZip.imageZip)
    from "scripts/linux/TOTPClient.sh"
}

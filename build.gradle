plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    id 'org.beryx.jlink' version '2.18.0'
    // JDKのみにしたい場合は"org.beryx.runtime"を使う
}

group 'info.repy'
version '1.0-SNAPSHOT'

mainClassName = "info.repy.totp/info.repy.totp.TOTPClient"

repositories {
    mavenCentral()
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

run {
    // add-modulesを探す用
    //jvmArgs = ['--limit-modules','java.base,javafx.controls,javafx.fxml,org.json,jdk.crypto.cryptoki']
}

jlink {
    // zipの"image/lib/modules"内にJDKとOpenJFXとアプリが合体して保存される
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', 'java.base']
    launcher {
        name = 'TOTPClient'
    }
}

dependencies {
    implementation group: 'org.jboss.aerogear', name:'aerogear-otp-java', version: '1.0.+'
}

task buildWindows(type: Zip, dependsOn: tasks.jlink) {
    archiveName = "TOTPClient.zip"
    into("image") {
        from fileTree(tasks.jlink.imageDir)
    }
    from "scripts/windows/TOTPClient.bat"
}

task buildMac(type: Tar, dependsOn: tasks.jlink) {
    compression = Compression.GZIP
    archiveName = "TOTPClient.tar.gz"
    into("TOTPClient.app"){
        from fileTree("scripts/mac")
        into("Contents/MacOS/image"){
            from fileTree(tasks.jlink.imageDir)
        }
    }
}

task buildLinux(type: Tar, dependsOn: tasks.jlink) {
    compression = Compression.GZIP
    archiveName = "TOTPClient.tar.gz"
    into("image") {
        from fileTree(tasks.jlink.imageDir)
    }
    from "scripts/linux/TOTPClient.sh"
}
